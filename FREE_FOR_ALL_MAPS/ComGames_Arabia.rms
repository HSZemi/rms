
/* Arabia for Forgotten Empires */

#include_drs random_map.def 54000

#const MONUMENT 826
#const SABOTEUR 706
#const SHARKATZOR 1222
#const TRADE_WORKSHOP 110
#const OLD_MONK_W_RELIC 1025
#const SHARK_ARROW 1223
#const TRISTAN 1109


/* ****************************************************** */
<PLAYER_SETUP>

random_placement

	effect_amount GAIA_ADD_ATTRIBUTE TREE_CLASS ATTR_STORAGE_VALUE 150

	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_TRAIN_LOCATION 82
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_TRAIN_BUTTON 14
	effect_amount MUL_ATTRIBUTE SABOTEUR ATTR_WOOD_COST -2
	effect_amount MUL_ATTRIBUTE SABOTEUR ATTR_FOOD_COST -1
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_GOLD_COST 35
	effect_amount ENABLE_OBJECT SABOTEUR ATTR_ENABLE 0
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_HITPOINTS 60
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_SHOWN_ATTACK 45
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_ATTACK 301
	effect_amount ADD_ATTRIBUTE SABOTEUR ATTR_ATTACK 3066
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_BLAST_RADIUS 2
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_BLAST_LEVEL 1
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_ARMOR 516
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_ARMOR 2
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_SHOWN_PIERCE_ARMOR 4
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_MOVE_SPEED 1.05
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_CREATE_SDESC_ID 6660
	effect_amount SET_ATTRIBUTE SABOTEUR ATTR_NAME_ID 5588

	effect_amount SET_ATTRIBUTE TRISTAN ATTR_TRAIN_LOCATION 826
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_TRAIN_BUTTON 1
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_SHOWN_ATTACK 14
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_ATTACK 526
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_ARMOR 258
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_SHOWN_MELEE_ARMOR 2
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_ARMOR 514
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_SHOWN_PIERCE_ARMOR 2
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_GOLD_COST 45
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_HITPOINTS 70
	effect_percent SET_ATTRIBUTE TRISTAN ATTR_MOVE_SPEED 90
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_CREATION_TIME 50
	effect_amount SET_ATTRIBUTE TRISTAN ATTR_NAME_ID 5779
	effect_amount ENABLE_OBJECT TRISTAN ATTR_ENABLE 0

	effect_amount SET_ATTRIBUTE KING ATTR_DEAD_ID 1222
	effect_amount SET_ATTRIBUTE OLD_MONK_W_RELIC ATTR_HITPOINTS 0
	effect_amount SET_ATTRIBUTE OLD_MONK_W_RELIC ATTR_DEAD_ID 922
	effect_amount SET_ATTRIBUTE SHARKATZOR ATTR_HITPOINTS 0
	effect_amount SET_ATTRIBUTE SHARKATZOR ATTR_DEAD_ID 1025

	effect_amount SET_ATTRIBUTE BOMBARD_TOWER ATTR_GOLD_COST 200
	effect_amount SET_ATTRIBUTE BOMBARD_TOWER ATTR_STONE_COST 250

	effect_percent SET_ATTRIBUTE TRADE_CART ATTR_MOVE_SPEED 70
	effect_percent SET_ATTRIBUTE TRADE_COG ATTR_MOVE_SPEED 93

	guard_state MONUMENT AMOUNT_GOLD 250 2
	ai_info_map_type ARABIA 0 0 0

/* ****************************************************** */
<LAND_GENERATION>

start_random
  percent_chance 50
  #define DESERT_MAP
end_random

if DESERT_MAP
  base_terrain                     DIRT
else           
  base_terrain                     GRASS3
endif

create_player_lands 
{ 
if DESERT_MAP
  terrain_type                     DIRT
else           
  terrain_type                     GRASS3
endif
  land_percent                       25
  base_size                           9
  border_fuzziness                   15
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* PRIMARY FOREST */

if DESERT_MAP
  create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 5
  land_percent                   12
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS3
  spacing_to_other_terrain_types 5
  land_percent                   12
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if DESERT_MAP
   create_terrain DESERT
{
  base_terrain                   DIRT
  number_of_clumps               16
  spacing_to_other_terrain_types 0
  land_percent                   20
  set_scale_by_size
}
else
create_terrain DIRT
{
  base_terrain                   GRASS3
  number_of_clumps               26
  spacing_to_other_terrain_types 1
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY FOREST */

if DESERT_MAP
create_terrain FOREST
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* SECONDARY PATCH */

if DESERT_MAP
   create_terrain DIRT3
{
  base_terrain                   DIRT
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS3
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if DESERT_MAP
   create_terrain GRASS3
{
  base_terrain                   DIRT
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DESERT
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

/* OASES */

if DESERT_MAP
   create_terrain WATER
{
  base_terrain                   PALM_DESERT
  spacing_to_other_terrain_types 1
  land_percent                   1
  number_of_clumps               8
  set_avoid_player_start_areas
  set_flat_terrain_only     
  set_scale_by_groups
}
endif

<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

if LUDIKRIS_MAP
create_object RELIC
{
   number_of_objects            250
   min_distance_to_players     25
   temp_min_distance_group_placement 110
}
else
create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
endif


if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    25
  max_distance_to_players    35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

create_object SHEEP
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    9
   max_distance_to_players    11
}
create_object SHEEP
{
   number_of_objects 2
   number_of_groups  2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects          4
percent_chance 50
  number_of_objects          3
end_random
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players    14
  max_distance_to_players    22
percent_chance 50
  min_distance_to_players    23
  max_distance_to_players    30
end_random
}

create_object BOAR
{
  number_of_objects          1
  set_loose_grouping  
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object BOAR
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object WOLF
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}

/* *****RESOURCES BY MAP***** */

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object GOLD
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object STONE
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

/* RESOURCES BY MAP */

if LARGE_MAP
create_object FORAGE
{
  number_of_groups           1    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_group_placement  7
  place_on_specific_land_id     21
}
endif

if HUGE_MAP
create_object FORAGE
{
  number_of_groups           2    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_group_placement  7
  place_on_specific_land_id     21
}
endif

if GIGANTIC_MAP
create_object FORAGE
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_group_placement  7
  place_on_specific_land_id     21
}
endif

if LUDIKRIS_MAP
create_object FORAGE
{
  number_of_groups           12    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  15
}
endif

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PALMTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
else
create_object OAKTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
endif


create_object HAWK
{
   number_of_objects         4
   set_scaling_to_map_size
}

create_object WOLF
{
  number_of_groups          3
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
else
create_object OAKTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
endif

if DESERT_MAP
create_object SHORE_FISH
{
  number_of_objects                    4
  min_distance_group_placement         5
  set_gaia_object_only
  set_scaling_to_map_size
}
endif

/* ****************************************************** */

<ELEVATION_GENERATION>

if TINY_MAP
create_elevation        7
{
  if DESERT_MAP
  base_terrain                     DIRT
else           
  base_terrain                     GRASS3
endif
  number_of_clumps 		20
  number_of_tiles 		3000
  set_scale_by_groups
  set_scale_by_size           
}
else
create_elevation        7
{
  if DESERT_MAP
  base_terrain                     DIRT
else           
  base_terrain                     GRASS3
endif
  number_of_clumps 		20
  number_of_tiles 		6000
  set_scale_by_groups
  set_scale_by_size           
}
endif


/* ****************************************************** */
<CLIFF_GENERATION>
min_number_of_cliffs 10
max_number_of_cliffs 15
min_length_of_cliff  3
max_length_of_cliff  10